; GAS MODEL TESTS

; ============================================================ ;
;; TEST: Out of gas error
;;
(env-exec-config ["DisableInlineMemCheck"])
(env-gaslimit 11)
(env-gasrate 1)
(map (+ 1) (make-list 8 8))
(expect-failure "out of gas" (+ 1 2))

; ============================================================ ;
;; SETUP: Load up simple module
;;
(begin-tx)
(env-gasmodel "table")
(env-gaslimit 30000)

(env-gas 0) ;; resets gas consumed
(env-gaslog) ;; brackets computation whose gas logs you want
(module basic-module GOV
  (defcap GOV () true)

  (defschema account-with-nickname
    balance:decimal
    nickname:string)

  (defschema account
    balance:decimal)

  (defschema obj-table
    name:object)

  (deftable accounts:{account})
  (deftable accounts-with-longname:{account})
  (deftable accounts-with-nickname:{account-with-nickname})
  (deftable objs-table:{obj-table})

  (defun transfer (senderId receiverId amount)
    "Send money from sender to receiver"
    ;;Read current account information of the sender
    (with-read accounts senderId {
      "balance":= senderBalance }
       ;;Read current account information of the receiver
       (with-read accounts receiverId {
         "balance":= receiverBalance }
         ;;Update account balance of the sender
         (update accounts senderId {
            "balance": (- senderBalance amount) })
         ;;Update account balance of the receiver
         (update accounts receiverId {
            "balance": (+ receiverBalance amount) }))))

  (defun balance (id)
    (at "balance" (read accounts id ["balance"]))
  )

  (defpact tester ()
    (step "dummy step")
    (step
      (yield { "r": "a" }))
    (step
      (resume {"r" := res0 }
       res0))
    (step
      (yield { "g": "b", "y": "b"}))
    (step
      (resume {"g" := res3 }
        res3))
  )
)
(env-gaslog) ;; ends computation bracket for gas logs
(expect "module gas" 295 (env-gas))
;(env-gas 0)
;(create-table accounts)
;(env-gaslog) ;["TOTAL: 259" "create-table:GUnreduced: 250" "create-table:GWrite:WriteTable: 9"]
;(expect "Cost: creating table with short name in same tx as module definition" 259 (env-gas))
(commit-tx)

; ============================================================ ;
;; TEST: Table and row creation
;;
(begin-tx)
(use basic-module)

(env-gaslog)
(env-gas 0)
(create-table accounts)
(env-gaslog) ;; ["TOTAL: 260" "create-table:GUnreduced: 250" "GOV:GUserApp: 1" "create-table:GWrite:WriteTable: 9"]
(expect "Cost: creating table with short name" 260 (env-gas))

(env-gas 0)
(env-gaslog)
(create-table accounts-with-longname)
(env-gaslog)
(expect "Cost: creating table with longer name" 262 (env-gas))

(env-gas 0)
(env-gaslog)
(create-table accounts-with-nickname)
(env-gaslog)
(expect "Cost: creating table with longer name, more columns" 262 (env-gas))

(env-gas 0)
(env-gaslog)
(insert accounts-with-nickname "a" {"balance": 1.0, "nickname": "a"})
(env-gaslog)
(expect "Cost: inserting simple row" 143 (env-gas))

(env-gas 0)
(env-gaslog)
(insert accounts-with-nickname "a-longer-key" {"balance": 1.0, "nickname": "b"})
(env-gaslog)
(expect "Cost: inserting row with longer key name" 146 (env-gas))

(env-gas 0)
(env-gaslog)
(insert accounts-with-nickname "c" {"balance": 1.0, "nickname": "aaa"})
(env-gaslog)
(expect "Cost: insert row with longer column value" 144 (env-gas))

(env-gas 0)
(env-gaslog)
(insert accounts "a" {"balance": 1.0})
(insert accounts "b" {"balance": 10.0})
(insert accounts "c" {"balance": 20.0})
(env-gaslog)
(commit-tx)

; ============================================================ ;
;; TEST: cost of transfer

(begin-tx)
(env-gas 0)
(env-gaslog)
(use basic-module)
(insert accounts "Alice" {"balance": 10.0 })
(insert accounts "Bob" {"balance": 0.0 })
(env-gaslog)

(env-gas 0)
(env-gaslog)
(transfer "Alice" "Bob" 5.0)
(env-gaslog)
(expect "Cost: simple transfer" 281 (env-gas))

(expect "Alice was debited 5" 5.0 (balance "Alice"))
(expect "Bob was credited 5" 5.0 (balance "Bob"))

(commit-tx)

; ============================================================ ;
;; TEST: CPU-intensive operation
;;
(begin-tx)

(env-gas 0)
(env-gaslog)
(make-list 10 "a")
(env-gaslog)
(expect "gas cost of short make-list" 2 (env-gas))

(env-gas 0)
(env-gaslog)
(make-list 1000 "a")
(env-gaslog)
(expect "gas cost of medium make-list" 59 (env-gas))

(env-gas 0)
(env-gaslog)
(make-list 5000 8)
(env-gaslog)
(expect "gas cost of longer make-list" 470 (env-gas))

(env-gas 0)
(env-gaslog)
(map (+ 1) (make-list 1000 8))
(env-gaslog)
(expect "Cost: Mapping over long list (make-list 1, map 4, + 1)" 1063 (env-gas))
(commit-tx)

; ============================================================ ;
;; TEST: Load simple module
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(module a G
  (defcap G () true)
)
(env-gaslog)
(expect "Cost: short module name, one member" 23 (env-gas))

(env-gas 0)
(env-gaslog)
(module simple-simple G
  (defcap G () true)
)
(env-gaslog)
(expect "Cost: longer module name, one member" 28 (env-gas))

(env-gas 0)
(env-gaslog)
(module b G
  (defcap G () true)
  (defun f () true)
)
(env-gaslog)
(expect "Cost: short module name, two members" 28 (env-gas))

(env-gas 0)
(env-gaslog)
(module c G
  (defcap G () true)
  (defun fun () true)
)
(env-gaslog)
(expect "Cost: short module name, two members, longer member name" 29 (env-gas))
(commit-tx)

; ============================================================ ;
;; TEST: Load simple interface
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(interface i
  (defconst G true)
)
(env-gaslog)
(expect "Cost: short interface name, one member" 23 (env-gas))

(env-gas 0)
(env-gaslog)
(interface my-interface
  (defconst G true)
)
(env-gaslog)
(expect "Cost: longer interface name, one member" 27 (env-gas))

(env-gas 0)
(env-gaslog)
(interface j
  (defconst G true)
  (defconst f true)
)
(env-gaslog)
(expect "Cost: short interface name, two members" 28 (env-gas))

(env-gas 0)
(env-gaslog)
(interface k
  (defconst G true)
  (defconst fun true)
)
(env-gaslog)
(expect "Cost: short interface name, two members, longer member name" 29 (env-gas))
(commit-tx)

; ============================================================ ;
;; TEST: Using module
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(use b)
(env-gaslog)
(expect "Cost: using module with short name and one member" 2 (env-gas))

(env-gas 0)
(env-gaslog)
(c.fun)
(env-gaslog)
(expect "Cost: using qualified module name with function" 2 (env-gas))

(env-gas 0)
(env-gaslog)
(use c)
(env-gaslog)
(expect "Cost: using module previously called" 1 (env-gas))
(commit-tx)

; ============================================================ ;
;; TEST: Using interface
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(module d G2
  (defcap G2 () true)
  (implements k)
  (defconst G3 k.G)
)
(env-gaslog)
(expect "Cost: using interface" 33 (env-gas))
(commit-tx)

(begin-tx)
(env-gas 0)
(env-gaslog)
d.G3
(env-gaslog)
(expect "Cost: using interface constant" 1 (env-gas))
(commit-tx)

; ============================================================ ;
;; TEST: Writing yield, defPact user app cost
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(use basic-module)
(env-gaslog)

(env-gas 0)
(env-gaslog)
(tester)
(env-gaslog)
(expect "Cost: defpact application" 1 (env-gas))

(env-gas 0)
(env-gaslog)
(continue-pact 1)
(env-gaslog)
(expect "Cost: defpact, yield 1 value, short keys/values" 21 (env-gas))

(env-gas 0)
(env-gaslog)
(continue-pact 2)
(env-gaslog)
(expect "Cost: defpact, resume 1 out of 1 yielded values" 4 (env-gas))

(env-gas 0)
(env-gaslog)
(continue-pact 3)
(env-gaslog)
(expect "Cost: defpact, yield 2 values" 39 (env-gas))

(env-gas 0)
(env-gaslog)
(continue-pact 4)
(env-gaslog)
(expect "Cost: defpact, resume 1 out of 2 yielded values" 5 (env-gas))

(commit-tx)

; ============================================================ ;
;; TEST: select
(begin-tx)
(use basic-module)

(env-gas 0)
(env-gaslog)
(select accounts ["balance"] (constantly true))
(env-gaslog) ;; GOV user app charge applied. And used for rest of transaction.
(expect "Cost: select with 1 out of 1 column filter" 212 (env-gas))

(env-gas 0)
(env-gaslog)
(select accounts [] (constantly true))
(env-gaslog) ;; GOV user app charge NOT applied.
(expect "Cost: select with empty column list" 211 (env-gas))

(env-gas 0)
(env-gaslog)
(select accounts (constantly true))
(env-gaslog) ;; GOV user app charge NOT applied.
(expect "Cost: select with no column filter" 211 (env-gas))

(env-gas 0)
(env-gaslog)
(select accounts-with-nickname ["balance"] (constantly true))
(env-gaslog)
(expect "Cost: select with 1 out of 2 column filter" 210 (env-gas))

(env-gas 0)
(env-gaslog)
(select accounts-with-nickname ["balance", "nickname"] (constantly true))
(env-gaslog)
(expect "Cost: select with 2 out of 2 column filter" 211 (env-gas))

(env-gas 0)
(env-gaslog)
(select accounts-with-nickname [] (constantly true))
(env-gaslog)
(expect "Cost: select with empty column list" 210 (env-gas))

(env-gas 0)
(env-gaslog)
(select accounts-with-nickname (constantly true))
(env-gaslog)
(expect "Cost: select with no colum filter" 210 (env-gas))

(commit-tx)

; ============================================================ ;
;; TEST: sort
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(sort [3 1 2])
(env-gaslog)
(expect "Cost: sort list" 3 (env-gas))

(env-gas 0)
(env-gaslog)
(sort ['age, 'name] [{'name: "Lin",'age: 30} {'name: "Val",'age: 25}])
(env-gaslog)
(expect "Cost: sort object with field" 5 (env-gas))

(env-gas 0)
(env-gaslog)
(sort (make-list 10 "a"))
(env-gaslog)
(expect "Cost: sort short list" 5 (env-gas))

(env-gas 0)
(env-gaslog)
(sort (make-list 1000 "a"))
(env-gaslog)
(expect "Cost: sort short list" 119 (env-gas))

(commit-tx)

; ============================================================ ;
;; TEST: concatenation
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(+ (make-list 11 "a") (make-list 5 "b"))
(env-gaslog)
(expect "Cost: concatenation of two lists" 21 (env-gas))
(commit-tx)

; ============================================================ ;
;; TEST: read data
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(use basic-module)
(env-gaslog)

(env-gas 0)
(env-gaslog)
(read accounts "a")
(env-gaslog)
(expect "Cost: read row, no column filter, 1 column schema" 12 (env-gas))

(env-gas 0)
(env-gaslog)
(read accounts "a" [])
(env-gaslog)
(expect "Cost: read row, empty list column filter, 1 column schema" 11 (env-gas))

(env-gas 0)
(env-gaslog)
(read accounts "a" ["balance"])
(env-gaslog)
(expect "Cost: read row, 1 column filter, 1 column schema" 11 (env-gas))

(env-gas 0)
(env-gaslog)
(read accounts-with-nickname "a")
(env-gaslog)
(expect "Cost: read, no column filter, 2 columns schema" 12 (env-gas))

(env-gas 0)
(env-gaslog)
(read accounts-with-nickname "a" [])
(env-gaslog)
(expect "Cost: read row, empty list column filter, 2 column schema" 12 (env-gas))

(env-gas 0)
(env-gaslog)
(read accounts-with-nickname "a" ["balance"])
(env-gaslog)
(expect "Cost: read row, 1 column filter, 2 column schema" 12 (env-gas))

(env-gas 0)
(env-gaslog)
(read accounts-with-nickname "a" ["balance", "nickname"])
(env-gaslog)
(expect "Cost: read row, 2 column filter, 2 column schema" 12 (env-gas))

(commit-tx)

; ============================================================ ;
;; TEST: create keysets and namespaces
;;
(begin-tx)
(env-data {"short-key": ["s"],
           "long-key": ["s", "a"] })

(env-gas 0)
(env-gaslog)
(define-keyset "a" (read-keyset "short-key"))
(env-gaslog)
(expect "Cost: defining keyset with short name" 55 (env-gas))

(env-keys ["s"])
(env-gas 0)
(env-gaslog)
(define-keyset "a" (read-keyset "short-key"))
(env-gaslog)
(expect "Cost: rotating keyset with short name" 56 (env-gas))

(env-gas 0)
(env-gaslog)
(define-keyset "a-key" (read-keyset "short-key"))
(env-gaslog)
(expect "Cost: defining keyset with longer name" 56 (env-gas))

(env-gas 0)
(env-gaslog)
(define-keyset "c" (read-keyset "long-key"))
(env-gaslog)
(expect "Cost: defining keyset with short name and longer keyset" 64 (env-gas))

(env-gas 0)
(env-gaslog)
(define-namespace "a" (keyset-ref-guard "a-key") (keyset-ref-guard "a-key"))
(env-gaslog)
(expect "Cost: defining namespace with short name" 62 (env-gas))

(env-keys [])
(env-keys ["s"])
(env-gas 0)
(env-gaslog)
(define-namespace "a" (keyset-ref-guard "a-key") (keyset-ref-guard "a-key"))
(env-gaslog)
(expect "Cost: rotating namespace with short name" 64 (env-gas))

(commit-tx)

; ============================================================ ;
;; TEST: using keysets and namespaces
;;
(begin-tx)
(env-keys ["s"])

(env-gas 0)
(env-gaslog)
(namespace "a")
(env-gaslog)
(expect "Cost: using namespace" 14 (env-gas))

(env-gas 0)
(env-gaslog)
(enforce-keyset "a")
(env-gaslog)
(expect "Cost: using keyset" 9 (env-gas))

(commit-tx)

; ============================================================ ;
;; TEST: writing objects
;;
(begin-tx)
(env-gas 0)
(env-gaslog)
(use basic-module)
(env-gaslog)

(env-gas 0)
(env-gaslog)
(create-table objs-table)
(env-gaslog)
(expect "Cost: Create table with obj schema" 261 (env-gas))

(env-gas 0)
(env-gaslog)
(insert objs-table "a" {"name": {"a": 1, "b": 2}})
(env-gaslog)
(expect "Cost: Insert row with small object" 144 (env-gas))

(env-gas 0)
(env-gaslog)
(insert objs-table "b" {"name": {"a": 1,
                                 "b": 2.0090909999999999999999,
                                 "d": "aaaaaaaaaaaaaaaaaaaaaaa",
                                 "f": [1 1 1 1 1 1 1 1 1 1 1 1],
                                 "g": {"aa": 1, "bb": 2, "cc": 3},
                                 "c": true,
                                 "e": (time "2016-07-22T12:00:00Z")}})
(env-gaslog)
(expect "Cost: Insert row with every (most) types of pact value" 318 (env-gas))

(commit-tx)

; ============================================================ ;
;; TEST: read-msg
;;
(begin-tx)
(env-data {"a": (make-list 100 0)})

(env-gas 0)
(env-gaslog)
(read-msg)
(env-gaslog)
(expect "Cost: reading small data object" 10 (env-gas))

(commit-tx)

; ============================================================ ;
;; TEST: new module cost, post fork.
;;
(begin-tx)
(env-gas 0)
(env-gaslimit 80000)
(env-exec-config [])
(module basic-module GOV
  (defcap GOV () true)

  ; Gas lower for const usage post-fork.
  (defconst NETWORK_FEE 0.001)
  (defconst MIN_FEE (+ NETWORK_FEE 0.1))
  (defconst BANK "bank")

  (defschema account-with-nickname
    balance:decimal
    nickname:string)

  (defschema account
    balance:decimal)

  (defschema obj-table
    name:object)

  (deftable accounts:{account})
  (deftable accounts-with-longname:{account})
  (deftable accounts-with-nickname:{account-with-nickname})
  (deftable objs-table:{obj-table})

  (defun transfer (senderId receiverId amount)
    "Send money from sender to receiver, charging a fee"
    ;;Read current account information of the sender
    (with-read accounts senderId {
      "balance":= senderBalance }
       ;;Read current account information of the receiver
       (with-read accounts receiverId {
         "balance":= receiverBalance }
         ;;Update account balance of the sender
         ;;Charge the sender the fee
         (update accounts senderId {
            "balance": (- senderBalance (+ amount MIN_FEE)) })
         ;;Update account balance of the receiver
         (update accounts receiverId {
            "balance": (+ receiverBalance amount) })

         (with-read accounts BANK {"balance" := bankBalance }
           ;;Collect the fee
           (update accounts BANK { "balance": (+ bankBalance MIN_FEE) })))))

  (defun balance (id)
    (at "balance" (read accounts id ["balance"]))
  )

  (defpact tester ()
    (step "dummy step")
    (step
      (yield { "r": "a" }))
    (step
      (resume {"r" := res0 }
       res0))
    (step
      (yield { "g": "b", "y": "b"}))
    (step
      (resume {"g" := res3 }
        res3))
  )
)

(expect "new module gas" 60841 (env-gas))
(commit-tx)
